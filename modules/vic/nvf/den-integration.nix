{
  den,
  lib,
  inputs,
  ...
}:
{
  den.lib.nvf.package =
    pkgs: vimAspect: ctx:
    (inputs.nvf.lib.neovimConfiguration {
      inherit pkgs;
      modules = [ (den.lib.nvf.module vimAspect ctx) ];
    }).neovim;

  den.lib.nvf.module =
    vimAspect: ctx:
    let
      # a custom `vim` class that forwards to `nvf.vim`
      vimClass =
        { aspect-chain, ... }:
        den._.forward {
          each = lib.singleton true;
          fromClass = _: "vim";
          intoClass = _: "nvf";
          intoPath = _: [ "vim" ];
          fromAspect = _: lib.head aspect-chain;
          adaptArgs = lib.id;
        };

      # workaround NVF `options` option
      # see: https://github.com/NotAShelf/nvf/issues/1469
      vimOpts.nvf = _: {
        imports = [ (lib.mkAliasOptionModule [ "vim" "opts" ] [ "vim" "options" ]) ];
      };

      aspect = den.lib.parametric.fixedTo ctx {
        includes = [
          vimClass
          vimAspect
          vimOpts
        ];
      };

      module = den.lib.aspects.resolve "nvf" [ aspect ] aspect;
    in
    module;
}
